
;;
; specific indicators for stylized facts




;;
; 
to-report indicators-stylized:cor-access-dev-alltimes
  let x reduce [list:concatenate (list ?1 ?2)] indicators:ts-indicator "indicators:patches-accessibilities"
  let y reduce [list:concatenate (list ?1 ?2)] indicators:ts-indicator "indicators:patches-delta-actives"
  report list:pearson-correlation x y
end


;;
; correlation between accessibility and population growth estimated on patches
to-report indicators-stylized:cor-access-dev
  let x [patch:current-accessibility] of patches
  let y [patch:delta-actives] of patches
  report list:pearson-correlation x y
end

;;
; moving window average
to-report indicators-stylized:cor-access-dev-mw
  report indicators:causal-moving-average "indicators-stylized:cor-access-dev" 15 
end

to-report indicators-stylized:cor-access-dev-avg
  report indicators:time-average "indicators-stylized:cor-access-dev"
end



;;
; correlation between accessibility and employments estimated on patches
to-report indicators-stylized:cor-access-employments
  let x [patch:current-accessibility] of patches
  let y [patch:employments] of patches
  report list:pearson-correlation x y
end


to-report indicators-stylized:cor-access-employments-avg
  report indicators:time-average "indicators-stylized:cor-access-employments"
end




;;
; correlation between accessibility and average travel time
to-report indicators-stylized:cor-access-time [weighted?]
  let avgdist []
  ifelse not weighted? [
    set avgdist map mean matrix:to-row-list global:effective-distance-matrix
  ][
    let atot sum [patch:actives] of patches let etot sum [patch:employments] of patches
    set avgdist [(patch:a-to-e-distance + patch:e-to-a-distance) / (2 * (atot) * (etot)) ] of patches
  ]
  let avgtime map [global:network-speed / ?] avgdist
  let sortaccess map [first ?] (sort-by [last ?1 < last ?2] ([(list patch:current-accessibility patch:number)] of patches))
  report list:pearson-correlation avgtime sortaccess
end


to-report indicators-stylized:cor-access-time-avg [weighted?]
  report indicators:time-average (word "indicators-stylized:cor-access-time " weighted?)
end



