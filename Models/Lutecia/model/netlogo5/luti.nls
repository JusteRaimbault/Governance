
;;;;;;;;;;;;;;
;; luti
;;;;;;;;;;;;;;



;;
; compute accessibilities, utilities and relocates actives and employments
to luti:luti
  
  output-print "luti module"
  
  ;; compute acc and utilities
  accessibilities:compute-patches-variables
  
  ;; update ext
  luti:update-externalities
  
  
  ;; relocate A and E
  luti:relocate
  
  ;; update wealths
  luti:update-wealth
  
end


;;
; Relocate employments and actives
;   given a DC model 
to luti:relocate

  if global:evolve-landuse? [

    output-print "... relocalization"
  
    ; discrete choice model following utilities for employments and actives
    let actives-number sum [patch:actives] of patches let employments-number sum [patch:employments] of patches
    let a-dc-norm-factor sum [exp (global:beta-discrete-choices * patch:a-utility)] of patches
    let e-dc-norm-factor sum [exp (global:beta-discrete-choices * patch:e-utility)] of patches
  
    set global:diff-actives 0 set global:diff-employments 0 set global:rel-diff-actives 0 set global:rel-diff-employments 0
  
    ask patches [
      let prev-a patch:actives let prev-e patch:employments
      set patch:actives ((1 - global:relocation-rate)* patch:actives) + (global:relocation-rate * actives-number * exp (global:beta-discrete-choices * patch:a-utility) / a-dc-norm-factor)
      set patch:employments ((1 - global:relocation-rate)* patch:employments) + (global:relocation-rate * employments-number * exp (global:beta-discrete-choices * patch:e-utility) / e-dc-norm-factor)
      set patch:delta-actives patch:actives - prev-a set patch:delta-employments patch:employments - prev-e
      set global:diff-actives global:diff-actives + abs (prev-a - patch:actives) set global:diff-employments global:diff-employments + abs (prev-e - patch:employments)
      set global:rel-diff-actives global:rel-diff-actives + ((abs (prev-a - patch:actives)) / patch:actives) set global:rel-diff-employments global:rel-diff-employments + ((abs (prev-e - patch:employments))/ patch:employments)
      ; update global lists
      set global:patches-employments-list replace-item patch:number global:patches-employments-list patch:employments
      set global:patches-actives-list replace-item patch:number global:patches-actives-list patch:actives
    ]
  
  ]
  
end



;;
; Aggregate wealth at mayor level : sum of employments (?)
to luti:update-wealth
  ask mayors [set mayor:wealth 0]
  ask patches [
    ask patch:governing-mayor [set mayor:wealth mayor:wealth + [patch:employments] of myself]
    ask global:regional-authority [set mayor:wealth mayor:wealth + [patch:employments] of myself]
  ]
  
end


;;
; update employments in external facilities, following an exponential growth
; 
to luti:update-externalities
  
  if global:with-externalities? [
    
    output-print "... externality"
    
    foreach global:external-facility [
       ask patch-with-number ? [
         set patch:employments global:ext-growth-factor * patch:employments * patch:current-accessibility / global:initial-max-acc
       ]
    ]
    
  ]
  
end
  
  
  
  

  
  