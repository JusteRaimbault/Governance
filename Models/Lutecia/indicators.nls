
;;;;;;;;;;;;;;;;;;;
;; Indicators of system performance
;;;;;;;;;;;;;;;;;;;

;-> convergence ? == stability ?
;-> distance moyenne deplacements == weighted mean by actives, employments of effective distance
;-> delta configs successives  ok diff-*
;-> hierarchy case, population, evol hierarchy.  -- TODO --


to indicators:update-indicators
  foreach global:tracked-indicators [
    let val run-result ?
    table:put global:history-indicators ? (lput val (table:get global:history-indicators ?))
  ]
end


to indicators:compute-indicators
  network:cache-nw-measures
  
  output-print (word "mean-accessibility : " indicators:mean-accessibility patches)
  output-print (word "mean-accessibility-balance : " indicators:mean-accessibility-balance)
  output-print (word "mean-travel-distance : " indicators:mean-travel-distance)
  output-print (word "actives-entropy : " indicators:morphology:entropy-actives)
  output-print (word "moran-actives : " indicators:morphology:moran-actives)
  output-print (word "collaboration wanted : " wanted-collaboration-rate)
  output-print (word "collaboration realized : " realized-collaboration-rate)
  output-print (word "externality-decrep : " externality-decrepitude)
  output-print (word "mean-congestion : " indicators:mean-congestion)
  output-print (word "total-nw-length : " indicators:network:total-nw-length)
  output-print (word "mean-relative-speed : " indicators:network:mean-relative-speed)
  output-print (word "mean-closeness-centrality : " indicators:network:mean-closeness-centrality)
  output-print (word "mean-bw-centrality : " indicators:network:mean-bw-centrality)
  output-print (word "mean-path-length : " indicators:network:mean-path-length)
  output-print (word "nw-diameter : " indicators:network:nw-diameter)
  output-print (word "alpha-bw-centrality : " indicators:network:alpha-bw-centrality)
  output-print (word "alpha-closeness-centrality : " indicators:network:alpha-closeness-centrality)
end

to indicators:compute-network-indicators
  network:cache-nw-measures
  ; total-nw-length mean-relative-speed mean-closeness-centrality mean-bw-centrality mean-path-length nw-diameter
  output-print (word "total-nw-length : " indicators:network:total-nw-length)
  output-print (word "mean-relative-speed : " indicators:network:mean-relative-speed)
  output-print (word "mean-closeness-centrality : " indicators:network:mean-closeness-centrality)
  output-print (word "mean-bw-centrality : " indicators:network:mean-bw-centrality)
  output-print (word "mean-path-length : " indicators:network:mean-path-length)
  output-print (word "nw-diameter : " indicators:network:nw-diameter)
end


;;
; TS indicators
to-report indicators:ts-indicator [indic]
  report table:get global:history-indicators indic
end




;;
; generic rank size hierarchy
to-report indicators:rank-size-hierarchy [x]
  if length x = 0 [report [0 0]]
  if max x = 0 and min x = 0 [report [0 0]]
  let sol matrix:regress matrix:from-column-list (list (map [log ? 10] sort-by [?1 >= ?2] x) (map [log ? 10] seq 1 length x 1))
  report (list (last first sol) (first last sol))
end




;;
; distance to target network
to-report indicators:target-network-distance
  ; kill potential previous target network
  ;ask transportation-links with [status = "ghost"] [ask both-ends [die] die]
  ;report network:network-distances transportation-links with [status = "initial"] transportation-links with [status = "ghost"]
  report network:network-distances transportation-links ghost-transportation-links
end



;;
; mean accessibility of employments and actives
to-report indicators:mean-accessibility [patchset]
  report accessibilities:mean-accessibility patchset
end

;;
; mean accessibility for all patches
to-report indicators:overall-mean-accessibility
  report accessibilities:mean-accessibility patches
end


;;
; mean accessibility for a given mayor
to-report indicators:mean-accessibility-mayor [i]
  report accessibilities:mean-accessibility (patches with [governing-mayor = mayor i])
end

;;
; Balance in the case of two mayors
to-report indicators:mean-accessibility-balance
  ifelse count mayors = 2 [
    report indicators:mean-accessibility-mayor 0 / indicators:mean-accessibility-mayor 1
  ][report 0]
end


; min / max access
to-report indicators:max-accessibility
   report max [max (list a-to-e-accessibility e-to-a-accessibility)] of patches
end

to-report indicators:min-accessibility
   report min [min (list a-to-e-accessibility e-to-a-accessibility)] of patches
end


; mean travel distance
to-report indicators:mean-travel-distance
  if (sum [actives] of patches) * (sum [employments] of patches) = 0 [report 0]
  report sum [a-to-e-distance + e-to-a-distance] of patches / (2 * (sum [actives] of patches) * (sum [employments] of patches))
end


;;
; TODO
; mean nw distance between nw patches
;to-report mean-network-distance
;  
;end


to-report indicators:mean-effective-distance
  if global:effective-distance-matrix = 0 [report 0]
  report mean map mean matrix:to-row-list global:effective-distance-matrix
end

; mean congestion in network
to-report indicators:mean-congestion
  if count transportation-links > 0 [report mean [congestion] of transportation-links]
  report 1
end

;;
; congestion on patches
to-report indicators:mean-patch-congestion
  report mean global:patches-congestion
end


;; last variability of relocation process
to-report indicators:stability
  report ((global:diff-employments / sum [employments] of patches) + (global:diff-actives / sum [actives] of patches) ) / 2
end


to-report indicators:mean-flow
  report matrix:mean global:flow-matrix
end

to-report indicators:min-flow
  report first matrix:min global:flow-matrix
end

to-report indicators:max-flow
  report first matrix:max global:flow-matrix
end


;;;;
;; Morphological indicators


to-report indicators:morphology:moran-actives
  report morphology:moran-actives
end

to-report indicators:morphology:entropy-actives
  report morphology:entropy-actives
end

to-report indicators:morphology:slope-actives
  report morphology:slope-actives
end

to-report indicators:morphology:slope-rsquared-actives
  report morphology:slope-rsquared-actives
end

to-report indicators:morphology:mean-distance-actives
  report morphology:mean-distance-actives
end

to-report indicators:morphology:moran-employments
  report morphology:moran-employments
end

to-report indicators:morphology:entropy-employments
  report morphology:entropy-employments
end

to-report indicators:morphology:slope-employments
  report morphology:slope-employments
end

to-report indicators:morphology:slope-rsquared-employments
  report morphology:slope-rsquared-employments
end

to-report indicators:morphology:mean-distance-employments
  report morphology:mean-distance-employments
end


;;
; mean distance to centre (to measure sprawl)
to-report indicators:mean-distance-to-centre-actives
  let atot sum [actives] of patches
  report sum [actives / atot * distance (patch 0 0)] of patches
end

to-report indicators:mean-distance-to-centre-employments
  let etot sum [employments] of patches
  report sum [employments / etot * distance (patch 0 0)] of patches
end

to-report indicators:center-actives-prop
  report [actives] of patch 0 0 / sum [actives] of patches
end


to-report indicators:center-employments-prop
  report [employments] of patch 0 0 / sum [employments] of patches
end



;;
;  Network indicators

;;
; nw diameter
to-report indicators:network:nw-diameter
  report network-indicators:nw-diameter
end

;;
; mean path length
to-report indicators:network:mean-path-length
  report network-indicators:mean-path-length
end

;;
;  bw centrality, normalized by number of paths considered
to-report indicators:network:mean-bw-centrality
  report network-indicators:mean-bw-centrality
end

;;
; hierarchy betwenness centrality
to-report indicators:network:alpha-bw-centrality
  report first indicators:rank-size-hierarchy ([bw-centrality] of transportation-links)
end

;;
; average closeness centrality
to-report indicators:network:mean-closeness-centrality
  let n count transportation-nodes if n = 0 [report 0]
  report (mean [transportation-node-closeness-centrality] of transportation-nodes) / n
end


;;
; hierarchy closeness
to-report indicators:network:alpha-closeness-centrality
  report first indicators:rank-size-hierarchy ([transportation-node-closeness-centrality] of transportation-nodes)
end


;;
; mean relative speed
to-report indicators:network:mean-relative-speed
  report network-indicators:mean-relative-speed
end

;;
; nw length
to-report indicators:network:total-nw-length
  report network-indicators:total-nw-length
end










;;
; Sum of employments in the external facility
to-report externality-employments
  ifelse global:external-facility = 0 or length global:external-facility = 0 [
    report 0 
  ][
    report sum [employments] of patches with [member? number global:external-facility]
  ]
end



to-report wanted-collaboration-rate
  ifelse length global:collaborations-wanted = 0 [
    report 0
  ][
    report sum global:collaborations-wanted / length global:collaborations-wanted
  ]
end


to-report realized-collaboration-rate
  ifelse length global:collaborations-wanted = 0 [
    report 0
  ][
    report sum global:collaborations-realized / length global:collaborations-realized
  ]
end

to-report expected-collaboration-rate
  ifelse length global:collaborations-wanted = 0 [
    report 0
  ][
    report mean global:collaborations-expected
  ]
end


to-report externality-decrepitude
  report externality-employments / (global:employments-max * global:ext-employments-proportion-of-max)
end
  



